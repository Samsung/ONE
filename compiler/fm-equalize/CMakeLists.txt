unset(FM_EQUALIZE_DEPS)

###
### Set up fm-equalize executable
###
set(FM_EQUALIZE_FILE "fm-equalize")
set(FM_EQUALIZE_SRC "${CMAKE_CURRENT_SOURCE_DIR}/${FM_EQUALIZE_FILE}")
set(FM_EQUALIZE_BIN "${CMAKE_CURRENT_BINARY_DIR}/${FM_EQUALIZE_FILE}")

add_custom_command(OUTPUT ${FM_EQUALIZE_BIN}
  COMMAND ${CMAKE_COMMAND} -E copy "${FM_EQUALIZE_SRC}" "${FM_EQUALIZE_BIN}"
  DEPENDS ${FM_EQUALIZE_SRC}
  COMMENT "Generate ${FM_EQUALIZE_BIN}"
)

list(APPEND FM_EQUALIZE_DEPS ${FM_EQUALIZE_BIN})

###
### Create fmelib directory
###
set(FM_EQUALIZE_PYTHON_DIR "fmelib")
set(FM_EQUALIZE_PYTHON_DIR_BIN "${CMAKE_CURRENT_BINARY_DIR}/${FM_EQUALIZE_PYTHON_DIR}")

add_custom_command(OUTPUT ${FM_EQUALIZE_PYTHON_DIR_BIN}
  COMMAND ${CMAKE_COMMAND} -E make_directory "${FM_EQUALIZE_PYTHON_DIR_BIN}"
  COMMENT "Generate ${FM_EQUALIZE_PYTHON_DIR_BIN}"
)

list(APPEND FM_EQUALIZE_DEPS ${FM_EQUALIZE_PYTHON_DIR_BIN})


###
### Set up Python files under fmelib
###
file(GLOB_RECURSE FM_EQUALIZE_PYTHON_FILES "${FM_EQUALIZE_PYTHON_DIR}/*.py")

foreach(FM_EQUALIZE_PYTHON_FILE IN ITEMS ${FM_EQUALIZE_PYTHON_FILES})
  get_filename_component(FILE_NAME ${FM_EQUALIZE_PYTHON_FILE} NAME)
  set(FM_EQUALIZE_PYTHON_FILE_SRC "${CMAKE_CURRENT_SOURCE_DIR}/${FM_EQUALIZE_PYTHON_DIR}/${FILE_NAME}")
  set(FM_EQUALIZE_PYTHON_FILE_BIN "${CMAKE_CURRENT_BINARY_DIR}/${FM_EQUALIZE_PYTHON_DIR}/${FILE_NAME}")

  add_custom_command(OUTPUT ${FM_EQUALIZE_PYTHON_FILE_BIN}
    COMMAND ${CMAKE_COMMAND} -E copy "${FM_EQUALIZE_PYTHON_FILE_SRC}" "${FM_EQUALIZE_PYTHON_FILE_BIN}"
    DEPENDS ${FM_EQUALIZE_PYTHON_SRC}
    COMMENT "Generate ${FM_EQUALIZE_PYTHON_FILE_BIN}"
  )

  list(APPEND FM_EQUALIZE_DEPS ${FM_EQUALIZE_PYTHON_FILE_BIN})
endforeach(FM_EQUALIZE_PYTHON_FILE)

add_custom_target(${FM_EQUALIZE_FILE} ALL DEPENDS ${FM_EQUALIZE_DEPS})

install(FILES ${FM_EQUALIZE_FILE}
        PERMISSIONS OWNER_WRITE OWNER_READ OWNER_EXECUTE
                    GROUP_READ GROUP_EXECUTE
                    WORLD_READ WORLD_EXECUTE
        DESTINATION bin)

install(DIRECTORY ${FM_EQUALIZE_PYTHON_DIR}
        FILE_PERMISSIONS OWNER_WRITE OWNER_READ
                          GROUP_READ
                          WORLD_READ
        DESTINATION bin)

if(NOT ENABLE_TEST)
  return()
endif(NOT ENABLE_TEST)

set(Python_ADDITIONAL_VERSIONS 3.8)
find_package(PythonInterp 3.8 QUIET)

if(NOT ${PYTHONINTERP_FOUND})
  message(FATAL_ERROR "Build fm-equalize_unittest: FAIL (Python3 is missing)")
endif()

add_test(
  NAME fm-equalize_unittest
  COMMAND ${PYTHON_EXECUTABLE} -m unittest
  WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
)
